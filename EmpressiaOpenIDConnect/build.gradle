plugins {
	id "java";
	id "signing";
	id "maven-publish";
}

ext.moduleName = "jp.empressia.enterprise.security.oidc";
// ext.upload_repository_url;
// ext.version;

repositories {
	jcenter();
}

dependencies {
	// use CDI.
	compileOnly(group:"javax.enterprise", name:"cdi-api", version:"2.0.SP1");
	// use Java EE Security API.
	compileOnly(group:"javax.security.enterprise", name: "javax.security.enterprise-api", version:"1.0");
	// use HttpServletRequest, HttpServletResponse
	compileOnly(group:"javax.servlet", name: "javax.servlet-api", version:"4.0.1");
	// use for security interceptor priority.
	compileOnly(group:"javax.annotation", name:"javax.annotation-api", version:"1.3.2");
	// use MicroProfile Config API.
	compileOnly(group:"org.eclipse.microprofile.config", name:"microprofile-config-api", version:"1.3");
	// use JCache for security token cache (not in Jave EE 8).
	compileOnly(group:"javax.cache", name:"cache-api", version:"1.1.1");
	// use for JWT.
	runtimeOnly(group:"io.jsonwebtoken", name:"jjwt-impl", version:"0.10.7");
	implementation(group:"io.jsonwebtoken", name:"jjwt-jackson", version:"0.10.7");
}

tasks.withType(JavaCompile) {
	options.encoding "UTF-8";
	options.compilerArgs << "-parameters";
}

java {
	withJavadocJar();
	withSourcesJar();
}
jar {
	archiveBaseName = moduleName;
	manifest {
		attributes (
			"Automatic-Module-Name": moduleName
		);
	}
}

javadoc {
	options.charSet "UTF-8";
	options.encoding "UTF-8";
	options.addBooleanOption("html5", true);
	options.addStringOption("Xdoclint:none", "-quiet");
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = "jp.empressia";
			artifactId = moduleName;
			from components.java;
			// for Maven Central.
			pom {
				name = "Empressia OpenID Connect";
				description = "OpenID Connect for Java EE Security (Jakarta Security).";
				url = "https://github.com/Empressia/EmpressiaOpenIDConnect";
				licenses {
					license {
						name = "zlib License";
						comments = "If you want to use this with a team (multiple people), select the MIT license.";
					}
					license {
						name = "MIT License";
						comments = "If you want to use this with a team (multiple people), select the MIT license.";
					}
				}
				developers {
					developer {
						// The unique ID of this developer in the SCM.
						id = "Empressia";
					}
				}
				scm {
					connection = "scm:git:https://github.com/Empressia/EmpressiaOpenIDConnect.git"
					url = "https://github.com/Empressia/EmpressiaOpenIDConnect.git"
				}
			}
		}
	}
	repositories {
		// publishMavenPublicationToLocalNetworkRepository
		maven {
			name = "LocalNetwork";
			url = local_network_repository_url;
		}
		// publishMavenPublicationToMavenCentralRepository
		maven {
			name = "MavenCentral";
			def releasesRepoUrl = sonatype_release_repository_url;
			def snapshotsRepoUrl = sonatype_snapshot_repository_url;
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl;
			credentials {
				username = sonatype_username;
				password = sonatype_password;
			}
		}
	}
}

signing {
	sign publishing.publications.maven;
}
